;Save_To_Sql=1
;Keep_Versions=5
;@Ahk2Exe-Let U_FileVersion = 0.0.4.9
;@Ahk2Exe-SetFileVersion %U_FileVersion%
;@Ahk2Exe-Let U_C = KAH - Viaweb
;@Ahk2Exe-SetDescription %U_C%
;@Ahk2Exe-SetMainIcon C:\AHK\icones\switch_collumn.ico

#Requires AutoHotkey v2.0
#Warn All, off
#SingleInstance Force

#Include C:\AutoHotkey\AHK V2\Sistema Monitoramento\libs\Functions.ahk
#Include C:\AutoHotkey\AHK V2\Sistema Monitoramento\libs\Class\ComboBoxFilter.ahk
#Include C:\AutoHotkey\AHK V2\Sistema Monitoramento\libs\Class\Json.ahk
#Include C:\AutoHotkey\AHK V2\Sistema Monitoramento\libs\Class\Unicode.ahk

Persistent

; ===================== CONFIGURA√á√ÉO =====================
	IP := "10.0.20.43"
	PORTA := 2700
	CHAVE := "94EF1C592113E8D27F5BB4C5D278BF3764292CEA895772198BA9435C8E9B97FD"
	IV := "70FC01AA8FCA3900E384EA28A5B7BCEF"

	SENHA_DEFAULT := "8790"

	POLL_INTERVAL_MS := 500
	GUI_UPDATE_MS := 1000

; ===================== CONSTANTES DE CORES =====================
	CORES := {
		ARMADA:				"00AA00",
		DESARMADA:			"FFAA00",
		DISPARADA:			"FF0000",
		CONECTADO:			"00FF00",
		DESCONECTADO:		"FF0000",
		INFO:				"0099FF",
		SUCESSO:			"00AA00",
		ERRO:				"FF0000",
		TEXTO_GRUPO:		"001f4e",
		TEXTO_ESCURO:		"000000",
		TEXTO_CLARO:		"FFFFFF",
		FUNDO_NEUTRAL:		"6e6d6d",
		FUNDO_INTERFACE:	"b1b1b1",
		BORDER_INFO:		"000000",
		; Cores dos sensores
		SENSOR_DISPARADO:	"FF0000",
		SENSOR_ABERTO:		"FFAA00",
		SENSOR_TAMPER:		"9932CC",
		SENSOR_TEMPORIZADO:	"FF8C00",
		SENSOR_INIBIDO:		"87CEEB",
		SENSOR_BATLOW:		"FF69B4",
		SENSOR_OK:			"00AA00"
	}

; ===================== VARI√ÅVEIS GLOBAIS =====================
	; Vari√°veis principais
		global client := 0
		global particionesStatus := Map()
		global zonasStatus := Map()
		global historicoMensagens := []
		global ultimaAtualizacao := ""
		global statusConexao := "Desconectado"
		global colorConexao := CORES.DESCONECTADO
		global guiHwnd := 0
		global gunidades := CarregarUnidadesDb()
		global lastZonasUpdateTick := 0
		global clientesMap := Map()

	; Controles de GUI
		global guiCtrlISEP := 0
		global guiCtrlStatusConexao := 0
		global guiCtrlTimestamp := 0
		global guiCtrlParticoes := []
		global guiCtrlParticoesText := 0
		global guiCtrlSensores := []
		global guiCtrlHistorico := 0
		global guiCtrlClienteSelecionado := 0

	; Buffers para recep√ß√£o
		global recvEncryptedAccum := Buffer(0)   ; acumula ciphertext entre recvs
		global recvPlainBuffer    := ""          ; acumula plaintext (JSON n√£o processado)

; ===================== CLASSES =====================

	; Classe base com os comandos do protocolo VIAWEB
	class Viaweb {

		static Version := "0.0.2"
		commandId := 0
		static GetVersion() {
			return this.Version
		}

		Send(jsonStr) {
			throw Error("Send must be implemented by subclass - ViawebClient")
		}

		GetCommandId() {
			this.commandId++
			return SubStr(SysGetIPAddresses()[1], -3) this.commandId
		}

		Identificar(nome := "AHK Monitor") {
			identJson := '{"a":' Random(1, 999999) ',"oper":[{"acao":"ident","nome":"' nome '"},{"acao":"salvarVIAWEB","operacao":2,"monitoramento":1}]}'
			OutputDebug('‚Üí ' identJson)
			this.Send(identJson)
		}

		Armar(idISEP, senha, particoes, forcado := 0) {
			idClean := RegExReplace(idISEP, "\D")
			if (idClean = "")
				idClean := idISEP
			cmdId := this.GetCommandId()
			if (Type(particoes) != "Array")
				particoes := [particoes]
			particoesStr := "[" JoinArray(particoes, ",") "]"
			cmdObj := '{"oper":[{"id":' cmdId ',"acao":"executar","idISEP":"' idClean '","comando":[{"cmd":"armar","password":"' senha '","forcado":' forcado ',"particoes":' particoesStr '}]}]}'
			OutputDebug('‚Üí ' cmdObj)
			this.Send(cmdObj)
			AddHistorico("üîí Armar: " JoinArray(particoes, ","), CORES.ARMADA)
			this.StatusParticoes(idISEP)
		}

		Desarmar(idISEP, senha, particoes) {
			idClean := RegExReplace(idISEP, "\D")
			if (idClean = "")
				idClean := idISEP
			cmdId := this.GetCommandId()
			if (Type(particoes) != "Array")
				particoes := [particoes]
			particoesStr := "[" JoinArray(particoes, ",") "]"
			cmdObj := '{"oper":[{"id":' cmdId ',"acao":"executar","idISEP":"' idClean '","comando":[{"cmd":"desarmar","password":"' senha '","particoes":' particoesStr '}]}]}'
			OutputDebug('‚Üí ' cmdObj)
			this.Send(cmdObj)
			AddHistorico("üîì Desarmar: " JoinArray(particoes, ","), CORES.DESARMADA)
			this.StatusParticoes(idISEP)
		}

		StatusParticoes(idISEP) {
			idClean := RegExReplace(idISEP, "\D")
			if (idClean = "")
				idClean := idISEP
			cmdId := this.GetCommandId()
			cmdObj := '{"oper":[{"id":' cmdId ',"acao":"executar","idISEP":"' idClean '","comando":[{"cmd":"particoes"}]}]}'
			OutputDebug('‚Üí ' cmdObj)
			this.Send(cmdObj)
			AddHistorico("üìã Consultando parti√ß√µes...`r`n`t`tcmdId: " cmdId "`r`n`t`tIdIsep: " idClean, CORES.INFO)
			this.StatusZonas(idISEP)
		}

		StatusZonas(idISEP) {
			idClean := RegExReplace(idISEP, "\D")
			if (idClean = "")
				idClean := idISEP
			cmdId := this.GetCommandId()
			cmdObj := '{"oper":[{"id":' cmdId ',"acao":"executar","idISEP":"' idClean '","comando":[{"cmd":"zonas"}]}]}'
			OutputDebug('‚Üí ' cmdObj)
			this.Send(cmdObj)
			AddHistorico("üìã Consultando zonas...`r`n`t`tcmdId: " cmdId "`r`n`t`tIdIsep: " idClean, CORES.INFO)
		}

		ListarClientes(portas := "", nomes := "", idISEPs := "") {
			cmdId := this.GetCommandId()

			oper := Map()
			oper["id"]   := cmdId
			oper["acao"] := "listarClientes"

			if (portas != "") {
				if (Type(portas) != "Array")
					portas := [portas]
				oper["porta"] := portas
			}

			if (nomes != "") {
				if (Type(nomes) != "Array")
					nomes := [nomes]
				oper["nome"] := nomes
			}

			if (idISEPs != "") {
				if (Type(idISEPs) != "Array")
					idISEPs := [idISEPs]
				oper["idISEP"] := idISEPs
			}

			payload := Map("oper", [oper])
			jsonStr := (StrReplace(JSON.stringify(payload, , ""), '`n', ' '))

			OutputDebug('‚Üí ' jsonStr)
			this.Send(jsonStr)

			AddHistorico("üìã Listar clientes enviado.`r`n`t`tcmdId: " cmdId, CORES.INFO)

		}
	}

	; Cliente: gerencia socket/cripto e herda os comandos do protocolo
	class ViawebClient extends Viaweb {
		socket := 0
		crypto := 0
		connected := false
		recvBuffer := Buffer(65536)
		commandId := 0
		hwnd := 0

		static Version := "0.1.1"

		static GetVersion() {
			return this.Version
		}

		__New(ip, port, hexKey, hexIV) {
			this.ip := ip
			this.port := port
			this.crypto := ViawebCrypto(hexKey, hexIV)
		}

		Connect() {
			wsaData := Buffer(408)
			if DllCall("ws2_32\WSAStartup", "UShort", 0x0202, "Ptr", wsaData)
				throw Error("WSAStartup falhou")

			this.socket := DllCall("ws2_32\socket", "Int", 2, "Int", 1, "Int", 6, "Ptr")
			if (this.socket = -1)
				throw Error("Falha ao criar socket")

			hostent := DllCall("ws2_32\gethostbyname", "AStr", this.ip, "Ptr")
			if (! hostent)
				throw Error("Falha ao resolver hostname")

			addrList := NumGet(hostent + (A_PtrSize = 8 ? 24 : 12), "Ptr")
			addr := NumGet(addrList, "Ptr")
			ipAddr := NumGet(addr, "UInt")

			sockAddr := Buffer(16, 0)
			NumPut("Short", 2, sockAddr, 0)
			NumPut("UShort", DllCall("ws2_32\htons", "UShort", this.port, "UShort"), sockAddr, 2)
			NumPut("UInt", ipAddr, sockAddr, 4)

			result := DllCall("ws2_32\connect", "Ptr", this.socket, "Ptr", sockAddr, "Int", 16, "Int")
			if (result = -1)
				throw Error("Falha ao conectar: " DllCall("ws2_32\WSAGetLastError", "Int"))

			modeBuf := Buffer(4)
			NumPut("UInt", 1, modeBuf, 0) ; 1 = non-blocking
			res := DllCall("ws2_32\ioctlsocket", "Ptr", this.socket, "UInt", 0x8004667E, "Ptr", modeBuf)
			if (res != 0) {
				err := DllCall("ws2_32\WSAGetLastError", "Int")
				DllCall("ws2_32\closesocket", "Ptr", this.socket)
				this.socket := 0
				throw Error("ioctlsocket falhou: " err)
			}

			this.connected := true
			FileAppend("[DEBUG] Socket: " this.socket " conectado (NON-BLOCKING)`n", A_ScriptDir "\debug.log")
			return true
		}

		Disconnect() {
			if (this.socket) {
				DllCall("ws2_32\closesocket", "Ptr", this.socket)
				DllCall("ws2_32\WSACleanup")
				this.socket := 0
				this.connected := false
			}
		}

		Send(jsonStr) {
			if (!this.connected)
				throw Error("N√£o conectado")

			encrypted := this.crypto.Encrypt(jsonStr)
			result := DllCall("ws2_32\send", "Ptr", this.socket, "Ptr", encrypted, "Int", encrypted.Size, "Int", 0, "Int")
			if (result = -1)
				throw Error("Falha ao enviar: " DllCall("ws2_32\WSAGetLastError", "Int"))
			return result
		}

		Poll() {
			if (!this.connected)
				return
			ListLines(0)
			Loop {
				recvBuf := Buffer(65536)
				received := DllCall("ws2_32\recv", "Ptr", this.socket, "Ptr", recvBuf, "Int", recvBuf.Size, "Int", 0, "Int")
				if (received < 0) {
					err := DllCall("ws2_32\WSAGetLastError", "Int")
					if (err = 10035)
						break
					FileAppend("[DEBUG] WSA Error: " err "`n", A_ScriptDir "\debug.log")
					if (err = 10054) {
						AddHistorico("‚ö†Ô∏è Conex√£o fechada.", CORES.ERRO)
						this.Disconnect()
						return
					}
					break
				}
				if (received = 0) {
					AddHistorico("‚ö†Ô∏è Conex√£o fechada pelo servidor.", CORES.ERRO)
					this.Disconnect()
					return
				}

				chunk := Buffer(received)
				Loop received
					NumPut("UChar", NumGet(recvBuf, A_Index-1, "UChar"), chunk, A_Index-1)

				global recvEncryptedAccum
				recvEncryptedAccum := CombineBuffers(recvEncryptedAccum, chunk)
			}
			ListLines(1)

			global recvEncryptedAccum, recvPlainBuffer
			if (!recvEncryptedAccum.Size)
				return

			fullBlocksBytes := Floor(recvEncryptedAccum.Size / 16) * 16
			if (fullBlocksBytes > 0) {
				procBuf := Buffer(fullBlocksBytes)
				ListLines(0)
				Loop fullBlocksBytes
					NumPut("UChar", NumGet(recvEncryptedAccum, A_Index-1, "UChar"), procBuf, A_Index-1)
					ListLines(1)
				leftoverSize := recvEncryptedAccum.Size - fullBlocksBytes
				if (leftoverSize > 0) {
					leftover := Buffer(leftoverSize)
					ListLines(0)
					Loop leftoverSize
						NumPut("UChar", NumGet(recvEncryptedAccum, fullBlocksBytes + A_Index-1, "UChar"), leftover, A_Index-1)
					ListLines(1)
					recvEncryptedAccum := leftover
				} else {
					recvEncryptedAccum := Buffer(0)
				}

				try {
					plaintext := this.crypto.Decrypt(procBuf)
					recvPlainBuffer := recvPlainBuffer . plaintext
				} catch Error as e {
					FileAppend("[DEBUG] Erro decrypt: " e.Message "`t" e.Extra "`t" e.Line "`n", A_ScriptDir "\debug.log")
					MsgBox("Erro ao descriptografar dados recebidos: " e.Message)
					return
				}
				ListLines(0)
				while (true) {
					nextJson := ExtractNextJsonFromBuffer()
					if (!nextJson)
						break
					try {
						ProcessarResposta(UnicodeHelper.Decode(nextJson))
						OutputDebug( '‚Üê ' UnicodeHelper.Decode(nextJson))
					} catch Error as e {
						FileAppend("[DEBUG] Erro ProcessarResposta: " e.Message "`nJSON:`n" UnicodeHelper.Decode(nextJson) "`n", A_ScriptDir "\debug.log")
					}
				}
				ListLines(1)
			}
		}
	}

	; Classe de criptografia via Bcrypt (AES CBC)
	class ViawebCrypto {
		hAlg := 0
		hKey := 0
		ivSend := Buffer(16)
		ivRecv := Buffer(16)
		blockSize := 16

		static Version := "0.0.1"

		static GetVersion() {
			return this.Version
		}

		__New(hexKey, hexIV) {
			key := this.HexToBytes(hexKey)
			iv := this.HexToBytes(hexIV)
			Loop 16 {
				NumPut("UChar", NumGet(iv, A_Index-1, "UChar"), this.ivSend, A_Index-1)
				NumPut("UChar", NumGet(iv, A_Index-1, "UChar"), this.ivRecv, A_Index-1)
			}

			result := DllCall("bcrypt\BCryptOpenAlgorithmProvider", "Ptr*", &hAlg := 0, "WStr", "AES", "Ptr", 0, "UInt", 0, "UInt")
			if (result != 0)
				throw Error("BCryptOpenAlgorithmProvider falhou: " Format("0x{:08X}", result))
			this.hAlg := hAlg

			chainMode := Buffer(StrPut("ChainingModeCBC", "UTF-16") * 2)
			StrPut("ChainingModeCBC", chainMode, "UTF-16")

			result := DllCall("bcrypt\BCryptSetProperty", "Ptr", this.hAlg, "WStr", "ChainingMode", "Ptr", chainMode, "UInt", chainMode.Size, "UInt", 0, "UInt")
			if (result != 0)
				throw Error("BCryptSetProperty falhou: " Format("0x{:08X}", result))

			result := DllCall("bcrypt\BCryptGenerateSymmetricKey", "Ptr", this.hAlg, "Ptr*", &hKey := 0, "Ptr", 0, "UInt", 0, "Ptr", key, "UInt", key.Size, "UInt", 0, "UInt")
			if (result != 0)
				throw Error("BCryptGenerateSymmetricKey falhou: " Format("0x{:08X}", result))
			this.hKey := hKey
		}

		__Delete() {
			if (this.hKey)
				DllCall("bcrypt\BCryptDestroyKey", "Ptr", this.hKey)
			if (this.hAlg)
				DllCall("bcrypt\BCryptCloseAlgorithmProvider", "Ptr", this.hAlg, "UInt", 0)
		}

		Encrypt(plainText) {
			plainBytes := this.StringToBytes(plainText)
			paddedSize := Ceil(plainBytes.Size / 16) * 16
			if (paddedSize = 0)
				paddedSize := 16

			paddedData := Buffer(paddedSize, 0)
			ListLines(0)
			Loop plainBytes.Size
				NumPut("UChar", NumGet(plainBytes, A_Index-1, "UChar"), paddedData, A_Index-1)
			ListLines(1)
			encrypted := Buffer(paddedSize)
			ivCopy := Buffer(16)
			ListLines(0)
			Loop 16
				NumPut("UChar", NumGet(this.ivSend, A_Index-1, "UChar"), ivCopy, A_Index-1)
			ListLines(1)
			bytesWritten := 0
			result := DllCall("bcrypt\BCryptEncrypt"
				, "Ptr", this.hKey
				, "Ptr", paddedData, "UInt", paddedSize
				, "Ptr", 0
				, "Ptr", ivCopy, "UInt", 16
				, "Ptr", encrypted, "UInt", encrypted.Size
				, "UInt*", &bytesWritten
				, "UInt", 0)
			if (result != 0)
				throw Error("BCryptEncrypt falhou: " Format("0x{:08X}", result))
			ListLines(0)
			Loop 16
				NumPut("UChar", NumGet(encrypted, paddedSize-16 + A_Index-1, "UChar"), this.ivSend, A_Index-1)
			ListLines(1)
			return encrypted
		}

		Decrypt(encryptedBuffer) {
			dataSize := encryptedBuffer.Size
			if (Mod(dataSize, 16) != 0)
				throw Error("Dados criptografados devem ter tamanho m√∫ltiplo de 16")

			lastBlock := Buffer(16)
			ListLines(0)
			Loop 16
				NumPut("UChar", NumGet(encryptedBuffer, dataSize-16 + A_Index-1, "UChar"), lastBlock, A_Index-1)
			ListLines(1)
			decrypted := Buffer(dataSize)
			ivCopy := Buffer(16)
			ListLines(0)
			Loop 16
				NumPut("UChar", NumGet(this.ivRecv, A_Index-1, "UChar"), ivCopy, A_Index-1)
			ListLines(1)
			bytesWritten := 0
			result := DllCall("bcrypt\BCryptDecrypt"
				, "Ptr", this.hKey
				, "Ptr", encryptedBuffer, "UInt", dataSize
				, "Ptr", 0
				, "Ptr", ivCopy, "UInt", 16
				, "Ptr", decrypted, "UInt", decrypted.Size
				, "UInt*", &bytesWritten
				, "UInt", 0)
			if (result != 0)
				throw Error("BCryptDecrypt falhou: " Format("0x{:08X}", result))
			ListLines(0)
			Loop 16
				NumPut("UChar", NumGet(lastBlock, A_Index-1, "UChar"), this.ivRecv, A_Index-1)
			ListLines(1)

			endPos := bytesWritten
			while (endPos > 0 && NumGet(decrypted, endPos-1, "UChar") = 0)
				endPos--

			return StrGet(decrypted, endPos, "UTF-8")
		}

		HexToBytes(hexStr) {
			hexStr := StrReplace(hexStr, " ", "")
			len := StrLen(hexStr) // 2
			buf := Buffer(len)
			ListLines(0)
			Loop len {
				byte := "0x" SubStr(hexStr, (A_Index - 1) * 2 + 1, 2)
				NumPut("UChar", Integer(byte), buf, A_Index-1)
			}
			ListLines(1)
			return buf
		}

		StringToBytes(str) {
			len := StrPut(str, "UTF-8") - 1
			buf := Buffer(len)
			StrPut(str, buf, "UTF-8")
			return buf
		}
	}

; ===================== FUN√á√ïES AUXILIARES =====================

	JoinArray(arr, sep := ",") {
		out := ""
		i := 1
		ListLines(0)
		Loop arr.Length {
			if (i > 1)
				out := out sep
			out := out arr[i]
			i++
		}
		ListLines(1)
		return out
	}

	CombineBuffers(b1, b2) {
		newSize := b1.Size + b2.Size
		newBuf := Buffer(newSize)
		idx := 0
		if (b1.Size) {
			ListLines(0)
			Loop b1.Size
				NumPut("UChar", NumGet(b1, A_Index-1, "UChar"), newBuf, idx++)
			ListLines(1)
		}
		if (b2.Size) {
			ListLines(0)
			Loop b2.Size
				NumPut("UChar", NumGet(b2, A_Index-1, "UChar"), newBuf, idx++)
			ListLines(1)
		}
		return newBuf
	}

	AddHistorico(message, color := "FFFFFF") {
		global historicoMensagens, guiHwnd, client
		timestamp := Format("{:02d}:{:02d}:{:02d}", A_Hour, A_Min, A_Sec)
		historicoMensagens.InsertAt(1, {message: message, color: color, timestamp: timestamp})

		if (historicoMensagens.Length > 50)
			historicoMensagens.Pop()
		
		if (guiHwnd && client && IsObject(client))
			AtualizarGUI()

	}

	ProcessaParticoes(resposta) {
		Global particionesStatus
		armado := resposta['armado']
		disparado := resposta['disparado']
		particao := resposta['pos']
		particionesStatus[particao] := Map('armado', armado, 'disparado', disparado)
		AddHistorico("‚úÖ Status da parti√ß√£o " particao " atualizado", CORES.SUCESSO)
	}

	ProcessaZonas(resposta) {
		Global zonasStatus, CORES, lastZonasUpdateTick
		aberta		:= resposta['aberta']
		batlow		:= resposta['batlow']
		disparada	:= resposta['disparada']
		inibida		:= resposta['inibida']
		pos			:= resposta['pos']
		tamper		:= resposta['tamper']
		temporizando:= resposta['temporizando']
		zonasStatus[pos] := Map('aberta', aberta, 'batlow', batlow, 'disparada', disparada, 'inibida', inibida, 'tamper', tamper, 'temporizando', temporizando)
		lastZonasUpdateTick := A_TickCount
		AddHistorico("‚úÖ Status do sensor " pos " atualizado`r`n`tAberto:`t`t" aberta "`r`n`tDisparado:`t" disparada "`r`n`tInibida:`t`t" inibida "`r`n`tTamper:`t`t" tamper "`r`n`tTemporizando:`t" temporizando, CORES.SUCESSO)
	}

	EnsureZonas(currIsep) {
		global client, ISEP_DEFAULT, lastZonasUpdateTick
		if (!client || !IsObject(client) || !client.connected)
			return
		; s√≥ refaz se ainda estamos no mesmo ISEP e zonas n√£o chegaram
		if (ISEP_DEFAULT != currIsep)
			return
		if ((A_TickCount - lastZonasUpdateTick) > 600) {
			try client.StatusZonas(currIsep)
		}
	}

	ResponderEvento(id) {
		global client
		respJson := '{"resp":[{"id":"' id '"}]}'
		client.Send(respJson)
	}

	ProcessarResposta(jsonStr) {
		response := json.parse(jsonStr)
		if	IsSet(response) && isObject(response) {
			if(response.Has("a")) {
				AddHistorico("‚ÑπÔ∏è Autenticado.`r`n`t`tEventos pendentes: " (response.Has("eventosPendentes") ? response["eventosPendentes"] : "0"), CORES.INFO)
				return
			}

			Try resp := response['resp']
			Switch {
				Case IsSet(resp):
					TratarResp(resp)
				Default:
					TratarResp(response)
			}
		}
	}

	ProcessaListarClientes(respItem) {
		global clientesMap
		clientesMap.Clear()
		if (!respItem.Has("viaweb"))
			return

		for vw in respItem["viaweb"] {
			servidorNome  := vw.Has("nome")  ? vw["nome"]  : ""
			servidorPorta := vw.Has("porta") ? vw["porta"] : ""

			if (vw.Has("cliente")) {
				for cli in vw["cliente"] {
					try {
						id := cli.Has("idISEP") ? cli["idISEP"] : ""
						if (id = "")
							continue

						; senhas: at√© 3 na ordem recebida
						pwd1 := "", pwd2 := "", pwd3 := ""
						pwd1Gen := "", pwd2Gen := "", pwd3Gen := ""
						if (cli.Has("senhas") && Type(cli["senhas"]) = "Array") {
							idx := 1
							for s in cli["senhas"] {
								if (idx = 1) {
									pwd1    := s.Has("senha")   ? s["senha"]   : ""
									pwd1Gen := s.Has("geracao") ? s["geracao"] : ""
								} else if (idx = 2) {
									pwd2    := s.Has("senha")   ? s["senha"]   : ""
									pwd2Gen := s.Has("geracao") ? s["geracao"] : ""
								} else if (idx = 3) {
									pwd3    := s.Has("senha")   ? s["senha"]   : ""
									pwd3Gen := s.Has("geracao") ? s["geracao"] : ""
								} else {
									break
								}
								idx++
							}
						}

						; meio: pega o primeiro, se existir
						offlineTypical := "", authorization := "", onlineTs := "", offlineTs := ""
						protection := "", delay := "", ping := "", ip := ""
						if (cli.Has("meio") && Type(cli["meio"]) = "Array" && cli["meio"].Length >= 1) {
							m := cli["meio"][1]
							offlineTypical := m.Has("offlineTipico") ? m["offlineTipico"] : ""
							authorization  := m.Has("autorizacao")   ? m["autorizacao"]   : ""
							onlineTs       := m.Has("online")        ? m["online"]        : ""
							offlineTs      := m.Has("offline")       ? m["offline"]       : ""
							protection     := m.Has("protecao")      ? m["protecao"]      : ""
							delay          := m.Has("atraso")        ? m["atraso"]        : ""
							ping           := m.Has("ping")          ? m["ping"]          : ""
							ip             := m.Has("ip")            ? m["ip"]            : ""
						}

						onlineFlag := cli.Has("online") ? cli["online"] : ""

						clientesMap[id] := Map()
						clientesMap[id].password1 := pwd1
						clientesMap[id].password2 := pwd2
						clientesMap[id].password3 := pwd3
						clientesMap[id].password1Generated := StrReplace(pwd1Gen, '\', '')
						clientesMap[id].password2Generated := StrReplace(pwd2Gen, '\', '')
						clientesMap[id].password3Generated := StrReplace(pwd3Gen, '\', '')
						clientesMap[id].offlineTypical := offlineTypical
						clientesMap[id].authorization := authorization
						clientesMap[id].onlineTimestamp  := (onlineTs  = "" ? "" : UnixToDateTime(onlineTs))
						clientesMap[id].offlineTimestamp := (offlineTs = "" ? "" : UnixToDateTime(offlineTs))
						clientesMap[id].protection := protection
						clientesMap[id].delay := delay
						clientesMap[id].ping := ping
						clientesMap[id].ip := ip
						clientesMap[id].online := onlineFlag

					} catch Error as e {
						AddHistorico("‚ùå Erro ao processar cliente: " (cli.Has("idISEP") ? cli["idISEP"] : "?") "`r`n`t" e.Message "`r`n`tLine: " e.Line, CORES.ERRO)
						continue
					}
				}
			}
			total := vw.Has("cliente") ? vw["cliente"].Length : 0
			AddHistorico("üìã Clientes armazenados: " total, CORES.INFO)
		}
	}

	TratarResp(respObj) {
		global client
		for index, item in respObj {
			if(! item.HasProp('Capacity'))
				continue
			if(item.Capacity = 0)
				continue

			; Tratamento de erros retornados em "resp"
			if(item.Has("erro")) {
				errCode := item["erro"]
				desc    := item.Has("descricao") ? item["descricao"] : "Erro"
				idStr   := item.Has("id") ? item["id"] : ""
				p1      := item.Has("param1") ? item["param1"] : ""
				p2      := item.Has("param2") ? item["param2"] : ""

				message := "‚ùå Erro (id: " idStr "): [" errCode "] " desc
				if (p1 != "")
					message .= "`r`n`tparam1: " p1
				if (p2 != "")
					message .= "`r`n`tparam2: " p2
				AddHistorico(message, CORES.ERRO)
				continue
			}

			; Tratamento de listarClientes (viaweb -> cliente)
			if(item.Has("viaweb")) {
				ProcessaListarClientes(item)
				continue
			}

			if(respObj.Has("oper")) {
				if(InStr(respObj['oper'][1]['acao'], 'evento') && InStr(respObj['oper'][1]['id'], '-evento')) {
					ResponderEvento(respObj['oper'][1]['id'])
					continue
				}
				ListLines(0)
				Loop respObj['oper'].Capacity
					for operIndex, operItem in respObj['oper'][A_Index] {
						OutputDebug(A_Now "`tOper " A_Index ": " operIndex " = " respObj['oper'][A_Index][operIndex])
					}
				ListLines(1)
			}

			if(item.Has("resposta")) {
				resposta := item['resposta']
				if(resposta.HasProp('Capacity'))	{
					ListLines(0)
					Loop resposta.Length {
						if(resposta[A_Index].Has("cmd")) {
							cmd := resposta[A_Index]['cmd']
							if		(cmd = 'particoes') {
								ProcessaParticoes(resposta[A_Index])
							}
							else if	(cmd = 'zonas') {
								ProcessaZonas(resposta[A_Index])
							}
							
						}
					}
					ListLines(1)
				}
				ResponderEvento(item['id'])
			}
		}
	}

	ExtractNextJsonFromBuffer() {
		global recvPlainBuffer
		s := recvPlainBuffer
		if (!s)
			return ""

		start := 0
		len := StrLen(s)
		ListLines(0)
		Loop len {
			ch := SubStr(s, A_Index, 1)
			if (ch = "{" || ch = "[") {
				start := A_Index
				break
			}
		}
		ListLines(1)
		if (!start) {
			recvPlainBuffer := ""
			return ""
		}
		s := SubStr(s, start)
		len := StrLen(s)
		depth := 0
		inStr := false
		i := 0
		ListLines(0)
		while (i < len) {
			i++
			ch := SubStr(s, i, 1)

			if (ch = '"') {
				bs := 0, j := i-1
				while (j >= 1 && SubStr(s, j, 1) = "\") {
					bs++, j--
				}
				if (Mod(bs, 2) = 0)
					inStr := !inStr
				continue
			}
			if (inStr)
				continue

			if (ch = "{" || ch = "[")
				depth++
			else if (ch = "}" || ch = "]") {
				depth--
				if (depth = 0) {
					candidate := SubStr(s, 1, i)
					try {
						_ := json.parse(candidate)
						recvPlainBuffer := SubStr(recvPlainBuffer, start + i)
						return candidate
					} catch Error as e {
						continue
					}
				}
			}
		}
		ListLines(1)
		return ""
	}

	PollTimer() {
		global client
		if (client && IsObject(client) && client.connected) {
			client.Poll()
		}
	}

	ObterStatusSensor(numSensor) {
		global zonasStatus, CORES
		
		if (! zonasStatus.Has(numSensor)) {
			return {
				texto: "Aguardando...",
				cor: CORES.FUNDO_NEUTRAL
			}
		}

		dados := zonasStatus[numSensor]
		
		if (dados["disparada"] = "1") {
			return {texto: "Disparado", cor: CORES.SENSOR_DISPARADO}
		} else if (dados["aberta"] = "1") {
			return {texto: "Aberto", cor: CORES.SENSOR_ABERTO}
		} else if (dados["tamper"] = "1") {
			return {texto: "Tamper", cor: CORES.SENSOR_TAMPER}
		} else if (dados["temporizando"] = "1") {
			return {texto: "Temporizado", cor: CORES.SENSOR_TEMPORIZADO}
		} else if (dados["inibida"] = "1") {
			return {texto: "Inibido", cor: CORES.SENSOR_INIBIDO}
		} else if (dados["batlow"] = "1") {
			return {texto: "Bateria Baixa", cor: CORES.SENSOR_BATLOW}
		} else {
			return {texto: "OK", cor: CORES.SENSOR_OK}
		}
	}

	ObterStatusParticao(numParticao) {
		global particionesStatus
		
		if (! particionesStatus.Has(numParticao)) {
			return {
				armado: "0",
				disparado: "0",
				texto: "Aguardando...",
				emoji: "‚è≥",
				cor: CORES.INFO
			}
		}

		dados := particionesStatus[numParticao]
		armado := dados["armado"]
		disparado := dados["disparado"]

		if (disparado = "1") {
			return {
				armado: armado,
				disparado: disparado,
				texto: "‚ö†Ô∏è DISPARADA",
				emoji: "üö®",
				cor: CORES.DISPARADA
			}
		} else if (armado = "1") {
			return {
				armado: armado,
				disparado: disparado,
				texto: "üîí ARMADA",
				emoji: "üîê",
				cor: CORES.ARMADA
			}
		} else {
			return {
				armado: armado,
				disparado: disparado,
				texto: "üîì DESARMADA",
				emoji: "üîì",
				cor: CORES.DESARMADA
			}
		}
	}

	AtualizarGUI() {
		global guiHwnd, particionesStatus, historicoMensagens, ultimaAtualizacao, statusConexao, colorConexao, client
		global guiCtrlStatusConexao, guiCtrlTimestamp, guiCtrlParticoes, guiCtrlHistorico, guiCtrlSensores
		static historicoMensagensLocal := Map()
		if(historicoMensagensLocal.HasOwnProp("Length") = false)
			historicoMensagensLocal.Length := 0
		if (!guiHwnd)
			return

		if (!client || !IsObject(client)) {
			statusConexao := "üî¥ DESCONECTADO"
			colorConexao := CORES.DESCONECTADO
			try guiCtrlStatusConexao.Text := statusConexao
			return
		}

		statusConexao := client.connected ? "üü¢ CONECTADO" : "üî¥ DESCONECTADO"
		colorConexao := client.connected ? CORES.CONECTADO : CORES.DESCONECTADO
		try guiCtrlStatusConexao.Text := statusConexao

		ultimaAtualizacao := Format("{:02d}:{:02d}:{:02d}", A_Hour, A_Min, A_Sec)
		try guiCtrlTimestamp.Text := "√öltima atualiza√ß√£o:`t" ultimaAtualizacao
		ListLines(0)
		Loop 8 {
			status := ObterStatusParticao(A_Index)
			guiCtrlParticoes[A_Index].Opt("-Redraw")
			guiCtrlParticoes[A_Index].Text := "Parti√ß√£o " A_Index ": " status.texto
			guiCtrlParticoes[A_Index].Opt("+Background" status.cor)
		}

		Loop 32 {
			status := ObterStatusSensor(A_Index)
			guiCtrlSensores[A_Index].Opt("-Redraw")
			guiCtrlSensores[A_Index].Text := A_Index ": " status.texto
			guiCtrlSensores[A_Index].Opt("+Background" status.cor)
		}

		if(IsObject(historicoMensagensLocal) && historicoMensagensLocal.Length != historicoMensagens.Length)	{
			historicoText := ""
			Loop historicoMensagens.Length {
				item := historicoMensagens[A_Index]
				historicoText := historicoText item.timestamp " - " item.message "`r`n"
			}
			try guiCtrlHistorico.Text := historicoText
			historicoMensagensLocal := historicoMensagens.Clone()
		}

		Loop 32 {
			if(A_Index < 9)
				guiCtrlParticoes[A_Index].Opt("+Redraw")
			guiCtrlSensores[A_Index].Opt("+Redraw")
		}
		ListLines(1)
	}

	UnixToDateTime(ts) {
		; ts: segundos desde 1970-01-01 00:00:00 UTC
		base := 19700101000000  ; YYYYMMDDhhmmss

		return DateAdd(base, ts, "s")  ; retorna YYYYMMDDhhmmss
	}
; ===================== INTERFACE GR√ÅFICA =====================
	CarregarUnidadesDb() {
		global gunidades, CORES
		unidades := []

		query := "SELECT [NOME],[NUMERO] FROM [Programa√ß√£o].[dbo].[INSTALACAO] ORDER BY 1"
		;query := "select * from [ASM].[dbo].[_unidades]"

		try {
			rs := sql(query)
			Loop rs.Length-1 {
				nome := rs[A_Index+1][1]
				num  := Format("{:04}",rs[A_Index+1][2])
				unidades.Push({ id : num, label : nome })
			}
		} catch Error as e {
			msgbox("‚ùå Erro SQL: " e.Message "`r`n`tLine: " e.Line, CORES.ERRO)
		}
		
		global ISEP_DEFAULT := rs[2][2] ' - ' rs[2][1]

		; Se nada voltou ou deu erro, mant√©m as unidades atuais
		if (unidades.Length = 0)
			return gunidades
		return unidades
	}

	CriarGUI() {
		global guiHwnd, guiCtrlStatusConexao, guiCtrlTimestamp, guiCtrlParticoes, guiCtrlSensores, guiCtrlHistorico, CORES
			, gunidades, guiCtrlISEP, guiCtrlClienteSelecionado
		MyGui := Gui()
		guiHwnd := MyGui.Hwnd

		MyGui.BackColor := CORES.FUNDO_INTERFACE
		MyGui.SetFont("S12")
		
		MyGui.Add("Text", "x15 Center w410 h20 cFFFFFF Background" CORES.INFO " Section", "üõ°Ô∏è VIAWEB MONITOR")
		MyGui.Add("Text", "x15 w410 h2 Background" CORES.BORDER_INFO, "")
		
		guiCtrlStatusConexao := MyGui.Add("Text", "Center w410 h20 c" CORES.CONECTADO " Background" CORES.FUNDO_NEUTRAL, "üî¥ DESCONECTADO")
		MyGui.SetFont("S9")

		MyGui.Add("Text", "x15 w410", "Endere√ßo:`t`t" IP ":" PORTA)

		MyGui.Add("Text", "x15 w60 y+5", "ISEP:")
		guiCtrlISEP := MyGui.Add("ComboBox", "x80 yp-3 w200", gunidades)
		filter := ComboBoxFilter(guiCtrlISEP, gunidades, true, true)
			global comboFilter := filter
		OnMessage(0x0100, ComboKeyNavBlock)    ; WM_KEYDOWN
		OnMessage(0x0111, HandleComboCommand)  ; WM_COMMAND
		guiCtrlISEP.Text := ISEP_DEFAULT

		guiCtrlClienteSelecionado := MyGui.Add("Text", "x15 y+5 w410 c" CORES.TEXTO_GRUPO, "Cliente selecionado: (nenhum)")
		ConfirmarISEP()

		guiCtrlTimestamp := MyGui.Add("Text", "x15 w410 y+0", "√öltima atualiza√ß√£o:`t00:00:00")
		MyGui.Add("Text", "x15 w410 h2 Background" CORES.BORDER_INFO, "")

		btnCtrlGroup := MyGui.Add("GroupBox", "x15 w410 h60 Section c" CORES.TEXTO_GRUPO, "üéÆ Controles de Central")
		MyGui.Add("Button", "x025 ys+20 w90 h30 c" CORES.TEXTO_CLARO " Background" CORES.ARMADA, "üîí Armar").OnEvent("Click", ArmarBtn)
		MyGui.Add("Button", "x125 ys+20 w90 h30 c" CORES.TEXTO_CLARO " Background" CORES.DESARMADA, "üîì Desarmar").OnEvent("Click", DesarmarBtn)
		MyGui.Add("Button", "x225 ys+20 w90 h30 c" CORES.TEXTO_CLARO " Background" CORES.INFO, "üìã Status").OnEvent("Click", StatusBtn)
		MyGui.Add("Button", "x325 ys+20 w90 h30 c" CORES.TEXTO_CLARO " Background" CORES.INFO, "üîÑ Zonas").OnEvent("Click", ZonasBtn)

		MyGui.Add("Text", "x15 w410 h2 Background" CORES.BORDER_INFO, "")

		MyGui.Add("GroupBox", "x15 y+10 w410 h165 Section c" CORES.TEXTO_GRUPO, "üìä Status das Parti√ß√µes ")
		Loop 8 {
			guiCtrlParticoes.Push("")
			status := ObterStatusParticao(A_Index)
			guiCtrlParticoes[A_Index] := MyGui.Add("Text", "x20 ys+" (A_Index * 16) " w390 h16 c" CORES.TEXTO_CLARO " 0x1500 Background" status.cor, "Parti√ß√£o " A_Index ": " status.texto)
		}

		MyGui.Add("Text", "x15 w410 h2 Background" CORES.BORDER_INFO, "")

		MyGui.Add("GroupBox", "x15 y+10 w410 h180 Section c" CORES.TEXTO_GRUPO, "üì° Status dos Sensores")
		yBaseSensores := 555
		Loop 32 {
			coluna := Mod(A_Index - 1, 4)
			linha := (A_Index - 1) // 4
			xPos := 20 + (coluna * 100)
			status := ObterStatusSensor(A_Index)
			guiCtrlSensores.Push(MyGui.Add("Text", "x" xPos " ys+" ((linha+1) * 20) " w95 h18 c" CORES.TEXTO_CLARO " 0x1500 Background" status.cor, A_Index ": " status.texto))
		}

		MyGui.Add("Text", "x15 w410 h2 Background" CORES.BORDER_INFO, "")

		MyGui.Add("GroupBox", "x15 w410 h120 c" CORES.TEXTO_GRUPO " Section", "üìú Hist√≥rico de A√ß√µes:")
		guiCtrlHistorico := MyGui.Add("Edit", "ys+20 xs+10 w390 h90 ReadOnly Multi Background" CORES.FUNDO_NEUTRAL " c" CORES.TEXTO_ESCURO)

		guiCtrlStatusConexao.Value := "üî¥ DESCONECTADO"
		guiCtrlTimestamp.Value := "√öltima atualiza√ß√£o:`t00:00:00"
		guiCtrlHistorico.Value := "Sistema iniciado`nAguardando conex√£o..."

		MyGui.Show("x0 y0")
		MyGui.OnEvent("Close", GuiClose)
		MyGui.Title := "üõ°Ô∏è VIAWEB Monitor - Dashboard de Monitoramento"
	}

	GetComboDropHwnd(cbHwnd) {
		cbInfo := Buffer(40, 0) ; COMBOBOXINFO tem 40 bytes em 64-bit/32-bit tamb√©m
		NumPut("UInt", cbInfo.Size, cbInfo, 0) ; cbSize
		if DllCall("user32\GetComboBoxInfo", "ptr", cbHwnd, "ptr", cbInfo) {
			offset := (A_PtrSize = 8) ? 24 : 16 ; hwndList offset
			return NumGet(cbInfo, offset, "ptr")
		}
		return 0
	}

	ComboKeyNavBlock(wParam, lParam, message, hwnd) {
		global guiCtrlISEP, comboFilter
		static VK_RETURN := 0x0D, VK_NUMPAD_RETURN := 0x0D
		static VK_DOWN := 0x28, VK_UP := 0x26
		static CB_GETDROPPEDSTATE := 0x157
		static CB_GETCURSEL := 0x147, CB_GETCOUNT := 0x146
		static CB_SETCURSEL := 0x14E, CB_FINDSTRINGEXACT := 0x158

		focusHwnd := DllCall("GetFocus", "ptr")
		dropHwnd  := GetComboDropHwnd(guiCtrlISEP.Hwnd)

		; s√≥ processa se o foco est√° no combo, no edit ou na listbox
		if (focusHwnd != guiCtrlISEP.Hwnd && focusHwnd != comboFilter.editHwnd && focusHwnd != dropHwnd)
			return

		dropped := DllCall("user32\SendMessageW", "ptr", guiCtrlISEP.Hwnd, "uint", CB_GETDROPPEDSTATE, "ptr", 0, "ptr", 0)

		; Enter / NumPadEnter com lista FECHADA confirma sele√ß√£o
		if (wParam = VK_RETURN || wParam = VK_NUMPAD_RETURN) {
			if (dropped)
				return  ; lista aberta: CBN_SELENDOK cuidar√°
			ConfirmarISEP()
			return 0
		}

		; Corrige a ‚Äúprimeira seta‚Äù com lista fechada sem mexer no texto do edit
		if (!dropped && (wParam = VK_DOWN || wParam = VK_UP)) {
			idx   := DllCall("user32\SendMessageW", "ptr", guiCtrlISEP.Hwnd, "uint", CB_GETCURSEL, "ptr", 0, "ptr", 0, "int")
			count := DllCall("user32\SendMessageW", "ptr", guiCtrlISEP.Hwnd, "uint", CB_GETCOUNT, "ptr", 0, "ptr", 0, "int")
			if (count > 0) {
				; se nada selecionado, tenta achar pelo texto atual
				if (idx = -1) {
					txt := guiCtrlISEP.Text
					found := DllCall("user32\SendMessageW", "ptr", guiCtrlISEP.Hwnd, "uint", CB_FINDSTRINGEXACT, "ptr", -1, "ptr", StrPtr(txt), "int")
					if (found >= 0)
						idx := found
				}
				if (idx = -1)
					idx := 0
				else if (wParam = VK_DOWN)
					idx := Min(idx + 1, count - 1)
				else if (wParam = VK_UP)
					idx := Max(idx - 1, 0)

				; aplica sele√ß√£o sem alterar guiCtrlISEP.Text
				DllCall("user32\SendMessageW", "ptr", guiCtrlISEP.Hwnd, "uint", CB_SETCURSEL, "ptr", idx, "ptr", 0)
				ConfirmarISEP()
				return 0
			}
		}

		; demais teclas seguem o padr√£o (digita√ß√£o/filtragem)
	}

	ConfirmarISEP() {
		global guiCtrlISEP, ISEP_DEFAULT, CORES, guiCtrlClienteSelecionado, client
		static lastISEP := ""  ; evita reprocessar o mesmo ISEP consecutivo

		if (!IsObject(guiCtrlISEP))
			return

		hwnd := guiCtrlISEP.Hwnd
		idx := DllCall("user32\SendMessageW", "ptr", hwnd, "uint", 0x147, "ptr", 0, "ptr", 0, "int") ; CB_GETCURSEL
		selText := ""

		if (idx != -1) {
			len := DllCall("user32\SendMessageW", "ptr", hwnd, "uint", 0x149, "ptr", idx, "ptr", 0, "int") ; CB_GETLBTEXTLEN
			buf := Buffer((len + 1) * 2, 0)
			DllCall("user32\SendMessageW", "ptr", hwnd, "uint", 0x148, "ptr", idx, "ptr", buf) ; CB_GETLBTEXT
			selText := StrGet(buf, "UTF-16")
		} else {
			selText := guiCtrlISEP.Text
		}

		idClean := RegExReplace(selText, "\D")
		if (idClean = "")
			return

		candidate := Format('{:04}', idClean)
		if (candidate = lastISEP)
			return  ; bloqueia repeti√ß√£o do mesmo ISEP

		ISEP_DEFAULT := candidate
		lastISEP := candidate

		AddHistorico("üìù ISEP selecionado: " ISEP_DEFAULT, CORES.INFO)
		if (IsObject(guiCtrlClienteSelecionado))
			guiCtrlClienteSelecionado.Text := "Cliente selecionado: " selText

		; ao selecionar cliente por qualquer caminho, chama Status
		if (client && IsObject(client) && client.connected) {
			try StatusBtn(0, 0)
		}
	}


	HandleComboCommand(wParam, lParam, msg, hwnd) {
		global guiCtrlISEP
		if (!IsObject(guiCtrlISEP))
			return

		static CBN_SELCHANGE := 1, CBN_SELENDOK := 9
		notify := (wParam >> 16) & 0xFFFF

		; Dispara somente quando a sele√ß√£o √© confirmada (clique ou Enter com lista)
		if (lParam = guiCtrlISEP.Hwnd && (notify = CBN_SELCHANGE || notify = CBN_SELENDOK)) {
			ConfirmarISEP()
			return 0
		}
	}

	GuiClose(GuiObj) {
		global guiHwnd
		SetTimer(PollTimer, 0)
		SetTimer(AtualizarGUI, 0)
		guiHwnd := 0
		ExitApp()
	}

	ArmarBtn(GuiCtrlObj, Info) {
		global client, ISEP_DEFAULT, SENHA_DEFAULT
		if (!client || !IsObject(client) || !client.connected) {
			AddHistorico("‚ùå N√£o conectado", CORES.ERRO)
			return
		}
		try {
			client.Armar(ISEP_DEFAULT, SENHA_DEFAULT, [1])
		} catch Error as e {
			AddHistorico("‚ùå Erro: " e.Message "`r`n`t" e.Extra "`r`n`tLine - " e.Line, CORES.ERRO)
		}
	}

	DesarmarBtn(GuiCtrlObj, Info) {
		global client, ISEP_DEFAULT, SENHA_DEFAULT
		if (!client || ! IsObject(client) || !client.connected) {
			AddHistorico("‚ùå N√£o conectado", CORES.ERRO)
			return
		}
		try {
			client.Desarmar(ISEP_DEFAULT, SENHA_DEFAULT, [1])
		} catch Error as e {
			AddHistorico("‚ùå Erro: " e.Message "`r`n`t" e.Extra "`r`n`tLine - " e.Line, CORES.ERRO)
		}
	}

	StatusBtn(GuiCtrlObj, Info) {
		global client, ISEP_DEFAULT, CORES, lastZonasUpdateTick
		static lastISEP := ""

		if (!client || !IsObject(client) || !client.connected) {
			AddHistorico("‚ùå N√£o conectado", CORES.ERRO)
			return
		}

		curr := ISEP_DEFAULT
		if (curr = lastISEP)
			return
		lastISEP := curr

		try {
			client.StatusParticoes(RegExReplace(curr, "\D"))
			; StatusParticoes j√° chama StatusZonas; se n√£o vier, tenta de novo depois de 700 ms
			SetTimer(() => EnsureZonas(curr), -700)
		} catch Error as e {
			AddHistorico("‚ùå Erro: " e.Message "`r`n`t" e.Extra "`r`n`tLine - " e.Line, CORES.ERRO)
		}
	}

	ZonasBtn(GuiCtrlObj, Info) {
		global client, ISEP_DEFAULT
		if (!client || !IsObject(client) || !client.connected) {
			AddHistorico("‚ùå N√£o conectado", CORES.ERRO)
			return
		}
		try {
			client.StatusZonas(ISEP_DEFAULT)
		} catch Error as e {
			AddHistorico("‚ùå Erro: " e.Message "`r`n`t" e.Extra "`r`n`tLine - " e.Line, CORES.ERRO)
		}
	}

	ISEPChanged(GuiCtrlObj, Info) {
		global ISEP_DEFAULT
		idClean := RegExReplace(GuiCtrlObj.Text, "\D")
		ISEP_DEFAULT := Format('{:04}', idClean)
		AddHistorico("üìù ISEP alterado para: " ISEP_DEFAULT, CORES.INFO)
	}
; ===================== INICIALIZA√á√ÉO ======================

	try {

		CriarGUI()

		client := ViawebClient(IP, PORTA, CHAVE, IV)
		client.Connect()

		AddHistorico("‚úÖ Conectado em " IP ":" PORTA, CORES.SUCESSO)

		client.Identificar("AHK Monitor GUI")
		AddHistorico("üîê Identifica√ß√£o enviada", CORES.INFO)

		SetTimer(PollTimer, POLL_INTERVAL_MS)
		SetTimer(AtualizarGUI, GUI_UPDATE_MS)

		client.ListarClientes()

	} catch Error as e {
		AddHistorico("‚ùå Erro na inicializa√ß√£o: " e.Message "`r`n`t" e.Extra "`r`n`tLine - " e.Line, CORES.ERRO)
		MsgBox("Erro: " e.Message "`n`t" e.Extra "`nLinha - " e.Line, "VIAWEB Monitor", "Icon!")
	}

; ===================== HOTKEYS =====================

	F3:: {
		StatusBtn(0, 0)
	}

	F4:: {
		Global client
		client.ListarClientes()
	}

	F1:: {
		ArmarBtn(0, 0)
	}

	F2:: {
		DesarmarBtn(0, 0)
	}

; ===================== EXIT ======================

	Shutdown(ExitReason, ExitCode) {
		global client
		SetTimer(PollTimer, 0)
		SetTimer(AtualizarGUI, 0)
		if (IsSet(client) && IsObject(client) && client.connected)
			client.Disconnect()
	}

	OnExit(Shutdown)	