;Save_To_Sql=1
;Keep_Versions=3
;@Ahk2Exe-Let U_FileVersion = 0.0.6.0
;@Ahk2Exe-SetFileVersion %U_FileVersion%
;@Ahk2Exe-Let U_C = KAH - Padronizador de Nome de Arquivos de Imagens
;@Ahk2Exe-SetDescription %U_C%
;@Ahk2Exe-SetMainIcon C:\AHK\icones\md_config.ico
#Requires AutoHotkey v2.0
#Warn All, Off
FileEncoding('UTF-8')

;	Includes
	#Include	C:\AutoHotkey\AHK V2\Sistema Monitoramento\libs\Functions.ahk2
	#Include	C:\AutoHotkey\AHK V2\Sistema Monitoramento\libs\Class\Email.ahk2
	#Include	C:\AutoHotkey\AHK V2\Sistema Monitoramento\libs\Class\Folder.ahk2
	OnMessage( 0x004A, readCommand )	;	ativa e desativa tooltip - VAR ShowTrayTip

;

;	Configurações

	if	A_IsCompiled {
		ext	:=	".exe"
		; auto_update(,1)
	}
	Else 
		ext	:=	".ahk"

	folders	:=	"intelbras,dahua,foscam,Motion"

	#SingleInstance Force
	; #NoTrayIcon
	if	A_IsCompiled
		sql_version(1,,datetime())
	current_folder	:=	0
	isnt_server		:=	A_UserName = "dsantos" ? 1 : 0
	motion_folder	:=	A_UserName = "dsantos" ? "\\srvftp\Monitoramento\FTP\Motion\"				:	"D:\FTP\Monitoramento\FTP\Motion\"
	ready_folder	:=	A_UserName = "dsantos" ? "\\srvftp\Monitoramento\FTP\Imagens Preparadas\"	:	"D:\FTP\Monitoramento\FTP\Imagens Preparadas\"
	FTP				:=	A_UserName = "dsantos" ? "\\srvftp\Monitoramento\FTP\"						:	"D:\FTP\Monitoramento\FTP\"
	size			:=	A_UserName = "dsantos" ? 8 : 7
	pre_load_foscam()
;

;	Code
	Loop {
		If( (A_Hour = 19 && A_Min = 00) && (A_Sec > 00 && A_Sec < 10) )	;	19:00:00 - 19:00:10 - reload do sistema
			Reload

		Sleep(1000)	;	roda de 1 em 1 segundo

		;	Main Folder
		OutputDebug "`tMain"
		Loop Files,	motion_folder '*.jpg', 'FDR'
			{
			If	!InStr( A_LoopFileFullPath, ".jpg" )	;	ignora diretórios
				continue
			path_part := StrSplit( A_LoopFileFullPath, "\" )
			file_name := StrReplace(A_LoopFileName, '.' A_LoopFileExt)
			Switch	{
				Case path_part.Length > Size && !InStr( A_LoopFileFullPath, "\Foscam" ):	;	folder em diretório não atualizável
					switch {
						case SubStr(file_name, 4, 1) = '_':
							file_name := RegExReplace(RegExReplace(SubStr(path_part[path_part.Length], 5,14), "\D" ),"(........)(......)", "$1-$2")
							;001_20241223130025_[M][0@0][0].jpg
						case StrLen(RegExReplace(file_name, "\D")) = 14:
							file_name := RegExReplace(RegExReplace(file_name, "\D"),'(........)(......)', '$1-$2')
							;20241223130025.jpg
						case StrLen(RegExReplace(file_name, "\D")) = 17:
							file_name := RegExReplace(RegExReplace(file_name, "\D"),'(........)(......)(...)', '$1-$2')
							;20241223130025_[M][0@0][0].jpg
						case StrLen(RegExReplace(file_name, "\D")) = 9:
							file_name := RegExReplace(RegExReplace(file_name, "\D"),'(......)(...)', '-$1')
							;10_2_247_209\2024-12-23\pic_001\13\13.04.37[M][0@0][0].jpg
						default:
							MsgBox file_name ' 2'
					}
					file_out:=	ready_folder
							.	RegexReplace(path_part[ InStr(path_part[6], "_") ? 6 : 7 ], "_", "." ) "_"
							.	RegexReplace(path_part[path_part.Length-3], "\D")
							.	file_name ".jpg"
					FileMove(A_LoopFileFullPath, file_out, 1)
				Default:
					If	!InStr( A_LoopFileFullPath, "\Foscam")	{
						filename := StrRep( A_LoopFileName, , ' ','_MD', '_Motion' )
						/* Ajusta horário para o padrão de data e hora*/
						f := StrSplit(filename, "-")
						filename := f[1] "-" ( StrLen(f[2]) > 10 ? SubStr(f[2], 1, 6) ".jpg" : f[2] )
						TRY FileMove(A_LoopFileFullPath, ready_folder filename, 1)
						last_folder	:= SubStr( A_LoopFileDir, InStr( A_LoopFileDir, "\", , -1)+1 )
						If( current_folder != last_folder && current_folder && last_folder != "Motion" )
							folder.Clear( A_LoopFileDir )
						current_folder	:=	last_folder
						OutputDebug "`tDahua - Default"
					}
				}

			}

		;	Foscam
		OutputDebug "`tFoscam"
		Loop Files, motion_folder 'Foscam\*.jpg', 'FDR'
			{
			p_foscam:=	StrSplit( A_LoopFileFullPath, "\" )
			mac		:=	SubStr( p_foscam[p_foscam.Length-2],	instr( p_foscam[p_foscam.Length-2], "_" )	+ 1 )
			data	:=	SubStr( p_foscam[p_foscam.Length],		InStr( p_foscam[p_foscam.Length], "_" )	+ 1 , 8 )
			hora	:=	SubStr( p_foscam[p_foscam.Length],		InStr( p_foscam[p_foscam.Length], "_" )	+ 10, 6 )
			ip		:=	foscam[ mac ]
			Switch	{
				Case StrLen( IP ) = 0:
					OutputDebug "`tFoscam - No Ip"
					FileMove(A_LoopFileFullPath, FTP	"AddBD\MAC - " mac " - " A_LoopFileName)
				Default:
					OutputDebug "`tFoscam - Default"
					TRY FileMove(A_LoopFileFullPath, ready_folder ip "_" data "-" hora ".jpg", 1 )
			}
		}
		Folder.Clear( ready_folder "Foscam" )
	}

Return

pre_load_foscam()	{
	Global FTP, foscam := Map()
	OutputDebug A_Now "`t PRELOAD FOSCAM"
		s	:='
			(
				SELECT
					c.[ip]
					,m.[mac]
					,c.[name]
					,c.[operador]
					,c.[sinistro]
					,LEFT( c.[vendormodel], charindex(' ', c.[vendormodel]) - 1)
				FROM
					[Dguard].[dbo].[cameras] c
				LEFT JOIN
					[Dguard].[dbo].[cameras_mac] m
				ON
					c.[ip] = m.[ip]
				WHERE
					LEFT( c.[vendormodel], charindex(' ', c.[vendormodel]) - 1) = 'Foscam'
			)'
			s := sql(s)
			
		If( ( s.Length-1 ) >= 1 ) {
			Loop	s.Length-1
				foscam[s[A_Index+1][2]]	:=	s[A_Index+1][1]
			FileAppend("Sucesso - " datetime() " Câmeras foscam carregadas = " s.Length-1 "`n", FTP "logs\Dados - Foscam.txt")
		}
		Else {
			Email.Send(	"dsantos@cotrijal.com.br"
					,	"Falha Servidor de Detecções - " Substr( datetime(), 1, 10 )
					,	"Busca SQL não retornou nenhuma câmera para montar o array de consulta`n`t`tPreparaImagens`n`n" sql_le "`n`n`t" sql_lq )
			FileAppend( "Falha - " datetime() "`n", FTP "logs\Dados - Foscam.txt")

		}
		Sleep(1000)
		OutputDebug A_Now "`t PRELOADED FOSCAM " foscam.Count

	}